import com.android.build.gradle.internal.tasks.FinalizeBundleTask

plugins {
    id 'com.android.application'
}

android {
    compileSdk 35
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "pl.waw.echo.choirbuddy"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode 6
        versionName "1.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file(System.getenv("SIGNING_STORE_PATH"))
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'pl.edu.mimuw.students.pl249278.android.musicinput'
    applicationVariants.all { variant ->
        def artifactName = "${rootProject.name}-${variant.name}-${variant.versionCode}-${variant.versionName}"
        variant.outputs.all {
            outputFileName = "${artifactName}.apk"
        }
        tasks.named("sign${variant.name.capitalize()}Bundle", FinalizeBundleTask) {
            File file = finalBundleFile.asFile.get()
            File finalFile = new File(file.parentFile, "${artifactName}.aab")
            finalBundleFile.set(finalFile)
        }
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation "androidx.core:core:${androidxCoreVersion}"
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation project(path: ':svgCommon')
    implementation name: 'joda-primitives-1.0'
    implementation name: 'plurals'
    implementation name: 'MIDICreator'
    implementation project(path: ':flowLayout')
    implementation project(path: ':asyncService')
    implementation project(path: ':logUtil')
    testImplementation 'junit:junit:4.+'
    androidTestImplementation "androidx.test.ext:junit:${androidJUnitVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${espressoVersion}"
    implementation 'com.google.guava:guava:30.1-android'

}
